module TEMPLATE_VOTING_REPAIR(TEMPLATE_COMMON_REPAIR) is

	process VOTING_K [IMPOSSIBLE : none,
	                  FAIL : NAT_CHANNEL,
	                  ACTIVATE : NAT_BOOL_CHANNEL,
	                  DEACTIVATE : NAT_BOOL_CHANNEL,
	                  ONLINE : NAT_CHANNEL]
		(threshold : NAT, total : NAT, repairable : NAT, always_active : BOOL)
	is var
		nr : NAT,
		done : BOOL,
		running : BOOL,
		nr_failed : NAT,
		received : BOOL_ARRAY,
		activated : BOOL_ARRAY,
		nr_act : NAT
	in
		received := BOOL_ARRAY(FALSE);
		activated := BOOL_ARRAY(FALSE);
		done := FALSE;
		nr_failed := 0;
		nr_act := 0;
		running := FALSE;
		loop
			while (nr_act > 0 and running and activated[nr_act]) loop
				if nr_act < total  then
					nr_act := nr_act + 1
				else
					nr_act := 0
				end if
			end loop;
			while (nr_act > 0 and not(running) and not(activated[nr_act])) loop
				nr_act := nr_act - 1
			end loop;

			select
			
			(*
			 * Listen for any child to be triggered.
			 * If this happens, we remember this by setting the boolean
			 * in the received array to TRUE. We also decrement the threshold.
			 *)
				FAIL (?nr) where (0 < nr) and (nr <= total);
				if not (received[nr]) then
					received[nr] := TRUE;
					nr_failed := nr_failed + 1
				end if
			
			(*
			 * If the threshold reaches 0, this means the threshold number
			 * of children have failed out of the total number of children.
			 * Thus, the Voting node fails and notifies its parents.
			 *)
			[]
				if (nr_failed >= threshold) and not (done) then
					FAIL (!0 of NAT); 
					done := TRUE
				end if
				
			-- increase decrease failed components if component gets repaired
			[]
				ONLINE (?nr) where (0 < nr) and (nr <= total);
				if ((nr > repairable) or (not(received[nr]))) then
					IMPOSSIBLE;
					loop null end loop
				else
					received[nr] := FALSE;
					nr_failed := nr_failed - 1
				end if

			-- send up signal if voting is working again
			[]
				if((done) and (nr_failed < threshold)) then 
					done := FALSE;
					ONLINE(!0 of NAT)
				end if

			(*
			 * If this node is activated, enable the activation procedure.
			 *)
			[]
				if not(always_active) then
					ACTIVATE (?nr,FALSE) where nr == (0 of NAT);
					nr_act := 1 of NAT;
					running := TRUE
				end if

			[]
				DEACTIVATE (0 of NAT,FALSE);
				if not(always_active) then
					nr_act := total;
					running := FALSE
				else
					IMPOSSIBLE;
					loop null end loop
				end if
			
			(*
			 * If the children should be activated, do so, one by one,
			 * from left to right.
			 *)
			[]
				if (nr_act > 0 and running) then
					activated[nr_act] := TRUE;
					ACTIVATE (!nr_act,TRUE);
					if nr_act < total  then
						nr_act := nr_act + 1
					else
						nr_act := 0
					end if
				end if

			(*
			 * If the children should be deactivated, do so, one
			 * by one, from right to left.
			 *)
			[]
				if (nr_act > 0 and not(running)) then
					activated[nr_act] := FALSE;
					DEACTIVATE (!nr_act,TRUE);
					nr_act := nr_act - 1
				end if
			end select
		end loop
	end var
	end process
end module 
